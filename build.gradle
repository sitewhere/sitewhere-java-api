plugins {
    id 'org.springframework.boot' version '2.4.4' apply false
    id 'com.github.hierynomus.license' version '0.15.0'
}

allprojects {
	group = 'com.sitewhere'
	version = '3.1.0'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'eclipse'

	apply plugin: "com.github.hierynomus.license"
	license {
		header rootProject.file('HEADER')
		include "**/*.java"
	}

	// Choose Java settings.
	apply plugin: 'java'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
		options.addStringOption('encoding', 'UTF-8')
		options.addStringOption('charSet', 'UTF-8')
	}

	// Common dependencies used for all projects.
	dependencies {
        // Sping dependency managment.
        implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        
		testImplementation group: 'junit', name: 'junit'
	}
	test {
		exclude '**/*'
	}
}