description = 'SiteWhere Java Client'

apply plugin: 'java'
dependencies {
    compile project(':sitewhere-java-model')
    
    // Android-compatible REST support.
    compile group: 'com.squareup.retrofit2', name: 'retrofit', version:'2.5.0'
    compile group: 'com.squareup.retrofit2', name: 'converter-jackson', version:'2.5.0'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.12.0'
   
    testCompile group: 'org.apache.activemq', name: 'activemq-all', version:'5.10.0'
}

license {
	exclude "**/Base64.java"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

// Publish jar.	
apply plugin: 'maven-publish'
publishing {
	publications {
		mavenJava(MavenPublication) {
		    from components.java
            artifact sourcesJar
            artifact javadocJar
			pom {
			    name = 'SiteWhere Java Client'
			    description = 'Java Client for SiteWhere REST APIs'
			    url = 'https://sitewhere.io'
			    licenses {
			        license {
			            name = 'CPAL-1.0'
			            url = 'https://opensource.org/licenses/CPAL-1.0'
			        }
			    }
                developers {
                    developer {
                        id = 'admin'
                        name = 'SiteWhere Admin'
                        email = 'admin@sitewhere.com'
                    }
                }
                scm {
                    connection = 'https://github.com/sitewhere/sitewhere-java-api.git'
                    url = 'https://github.com/sitewhere/sitewhere-java-api'
                }
			}	
		}
	}
    repositories {
        maven {
            url project.nexusUrl
            credentials {
                username project.nexusUsername
                password project.nexusPassword
            }
        }
    }
}

apply plugin: 'signing'
signing {
    sign publishing.publications.mavenJava
}
